set prompt,redirect
################################################################################
#
# krval_par.mac
#
################################################################################
#
#  Displays the R-value as function of parameter value for each member 
#  of the population.
#  If the current generation is chosen, the trial values for the next generation
#  are plotted at the bottom of the plot.
#
#  @krval_par generation,parameter
#             generation may be any generation number or -1 for the current one
#             
#
################################################################################
#
variable integer,generation
variable integer,member
variable integer,children
variable integer,parameters
variable integer,kid
fclose all
#
rese
#
# Get generation numbers, number of members, children and parameters
#
fopen 1,GENERATION
#
fget  1,generation,member,children,parameters
fclose 1
#
if($1.eq.-1) then
  i[0] = generation - 1
else
  i[0] = $1
endif
#
rese
load st, DIFFEV/Parameter, i[0],$2 +2,2
#
tit1 Correlation between parameter and R-value
if($1.eq.-1) then
  i[0] = generation - 1
else
  i[0] = $1
endif
#
tit2 "Generation %4d; Parameter Nr. %d",i[0],$2
#
ltyp 1,0
mtyp 1,3
lcol 1,1
mcol 1,1
#
r[0] = (ymax[1]-ymin[1])*0.05
r[1] = ymin[1]-r[0]
#
# If the current generation is chosen, plot trial values as well
#
if($1.eq.-1) then
  alloc trials,children
  ltyp 2,0
  mtyp 2,3
  lcol 2,3
  mcol 2,3
  do kid=1,children
    fopen 1,"DIFFEV/Versuche.%4D",kid
    fget 1
    fget 1
    fget 1
    fget 1
    fget 1
    do i[0]=1,$2
      fget 1,x[2,kid]
    enddo
    y[2,kid] = r[1]
    fclose 1
  enddo
endif
#
skal xmin[1],xmax[1],ymin[1]-r[0],ymax[1]+r[0]
plot
set prompt,on
